plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.github.OminousOne'
version = '1.0.0'

java.sourceCompatibility = 21
java.targetCompatibility = 21
compileJava.options.encoding = 'UTF-8'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly 'net.minestom:minestom-snapshots:7ce047b22e'
    testImplementation 'net.minestom:minestom-snapshots:7ce047b22e'
    //testImplementation 'com.github.TogAr2:MinestomFluids:b237b13a4b'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = 'MinestomPvP'
            groupId = "io.github.OminousOne"

            // Add basic POM info to avoid GitHub Packages validation issues
            pom {
                name.set("MinestomPvP")
                description.set("Minestom PvP modifications project")
                url.set("https://github.com/OminousOne/MinestomPvP")

                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }

                developers {
                    developer {
                        id.set("OminousOne")
                        name.set("Julien")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/OminousOne/MinestomPvP.git")
                    developerConnection.set("scm:git:ssh://github.com:OminousOne/MinestomPvP.git")
                    url.set("https://github.com/OminousOne/MinestomPvP")
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/OminousOne/MinestomPvP") // Update with your GitHub info

            // Authentication: Use these environment variables or Gradle properties
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")  // Username (GitHub)
                password = project.findProperty("gpr.token") ?: System.getenv("BUILD_TOKEN")  // Token (GitHub PAT)
            }
        }
    }
}
